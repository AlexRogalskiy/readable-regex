group = "net.codebox"
version = "1.0.0"

apply plugin: 'java'
apply plugin: 'maven'
apply plugin: 'signing'

repositories {
    mavenCentral()
}

dependencies {
    compile group: 'junit', name: 'junit', version: '4.11'

    testCompile group: 'org.hamcrest', name: 'hamcrest-all', version: '1.3'
}

signing {
    sign configurations.archives
}

task javadocJar(type: Jar) {
    classifier = 'javadoc'
    from javadoc
}

task sourcesJar(type: Jar) {
    classifier = 'sources'
    from sourceSets.main.allSource
}

artifacts {
    archives javadocJar, sourcesJar
}

uploadArchives {
    repositories {
        mavenDeployer {
            beforeDeployment { MavenDeployment deployment -> signing.signPom(deployment) }

            repository(url: "https://oss.sonatype.org/service/local/staging/deploy/maven2") {
                authentication(userName: ossrhUsername, password: ossrhPassword)
            }

            snapshotRepository(url: "https://oss.sonatype.org/content/repositories/snapshots") {
                authentication(userName: ossrhUsername, password: ossrhPassword)
            }

            pom.project {
                name 'readable-regex'
                packaging 'jar'
                artifactId 'readable-regex'
                description 'This library provides a way to make complex regular expressions in Java code more readable.'
                url 'https://github.com/codebox/readable-regex'

                scm {
                    connection 'scm:git@github.com:codebox/readable-regex.git'
                    developerConnection 'scm:git@github.com:codebox/readable-regex.git'
                    url 'https://github.com/codebox/readable-regex'
                }

                licenses {
                    license {
                        name 'MIT License'
                        url 'https://opensource.org/licenses/MIT'
                    }
                }

                developers {
                    developer {
                        id 'codebox'
                        name 'Rob Dawson'
                        email 'rob@codebox.net'
                    }
                }
            }
        }
    }
}